buildscript {
  ext {
    versionKotlin = '1.2.21'
    versionSpringBoot = '2.0.0.BUILD-SNAPSHOT'
    versionWro4j = '1.8.0.Beta4'

    versionBootstrap = '4.0.0'
    versionJQuery = '3.3.1'
    versionPopper = '1.11.1'
  }
  repositories {
    jcenter()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
  }
  dependencies {
    classpath("ro.isdc.wro4j.gradle:wro4j-gradle-plugin:${versionWro4j}")
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${versionSpringBoot}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${versionKotlin}")
    classpath("org.jetbrains.kotlin:kotlin-allopen:${versionKotlin}")
  }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'wro4j'
apply plugin: 'io.spring.dependency-management'

group = 'com.robbyp'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

repositories {
  jcenter()
  maven { url 'https://repo.spring.io/snapshot' }
  maven { url 'https://repo.spring.io/milestone' }
}

webResources {
  bundle ('core') {
    js 'js/**/*.js'
    preProcessor 'jsMin'
  }
  bundle ('libs') {
    js "webjars/bootstrap/$versionBootstrap/js/bootstrap.js"
    js "webjars/popper/$versionPopper/dist/popper.js"
  }
  bundle ('theme-default') {
    css "webjars/bootstrap/$versionBootstrap/css/bootstrap.css"
    css 'themes/default/main.css'

    cssOverrideImport 'variables.less', '../../../../themes/default/variables.less'
    //preProcessor 'less4j'
    cssRewriteUrl()
  }
  assets {
    include 'themes/default/images/**'
  }
}

dependencies {
  compile 'com.fasterxml.jackson.module:jackson-module-kotlin'
  compile 'org.jetbrains.kotlin:kotlin-stdlib-jre8'
  compile 'org.jetbrains.kotlin:kotlin-reflect'
  compile 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
  compile 'org.springframework.boot:spring-boot-starter-webflux'
  compile 'org.webjars:webjars-locator:0.32-1'

  testCompile 'org.springframework.boot:spring-boot-starter-test'
  testCompile 'io.projectreactor:reactor-test'

  webjarsRuntime ("org.webjars:bootstrap:$versionBootstrap")
  webjarsRuntime ("org.webjars:jquery:$versionJQuery")
}
